plugins {
    id 'groovy'
    id 'application'
    id "com.bertramlabs.asset-pipeline" version "3.4.4"
}

group 'xyz.itbang'
version '0.1'

sourceCompatibility = 1.8
mainClassName = "Application"

repositories {
    maven { url 'https://maven.aliyun.com/repository/public' }
    mavenCentral()
}

dependencies {
    implementation project(':grin')
    // compile fileTree(dir: 'libs', include: '*.jar')
    implementation 'org.codehaus.groovy:groovy-all:3.0.13'
    implementation 'io.undertow:undertow-servlet:2.2.21.Final'
    implementation 'io.undertow:undertow-websockets-jsr:2.2.21.Final'
    implementation 'org.thymeleaf:thymeleaf:3.0.15.RELEASE'

    implementation 'com.alibaba:druid:1.2.15'
    implementation 'com.zaxxer:HikariCP:5.0.1'

    runtimeOnly 'org.postgresql:postgresql:42.5.0'
    runtimeOnly 'com.h2database:h2:2.1.214'
    runtimeOnly 'org.slf4j:slf4j-api:2.0.4'
    runtimeOnly 'ch.qos.logback:logback-classic:1.3.5'

    testImplementation 'junit:junit:4.13.2'
}

// ---  grin ---
sourceSets {
    main {
        groovy {
            srcDir "grin-app/domains"
            srcDir "grin-app/controllers"
            srcDir "grin-app/websockets"
            srcDir "grin-app/views"
            srcDir "grin-app/scripts"
            srcDir "grin-app/init"
        }
        resources {
            srcDir "grin-app/conf"
        }
    }
}

// 运行 grin 提供的内部命令
task grin(type: JavaExec) {
    group 'grin'
    setStandardInput(System.in)
    jvmArgs('-Dfile.encoding=UTF-8')
    classpath = sourceSets.main.runtimeClasspath
    main = 'grin.app.Main'
}

// 部署运行准备，把编译后的文件，放到特定文件夹下
task prepare(type: Copy) {
    group 'grin'
    dependsOn classes, assetCompile
    into "$buildDir/grin"
    from sourceSets.main.runtimeClasspath
}

// 复制 grin 框架类库过来
task copyLibs(type: Copy) {
    group 'grin'
    from '/Users/yan/projects/grin-dev/grin/build/libs/'
    into "libs"
}

// assets 配置
assets {
    assetsPath = "grin-app/assets"
    // includes = []
    // excludes = ['**/*.less'] //Example Exclude GLOB pattern
    // from '/vendor/lib'
}
// --- grin ---